apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.wolkabout.hexiwear"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 30
        versionName "1.30"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()
                ]
            }
        }
    }

    productFlavors {
        staging {
            buildConfigField 'String', 'MQTT_HOST', '"ssl://staging.wolksense.com:8883"'
        }

        production {
            buildConfigField 'String', 'MQTT_HOST', '"ssl://wolksense.com:8883"'
        }
    }

    // Specifies one flavor dimension.
    flavorDimensions "version"
    productFlavors {
        staging {
            // Assigns this product flavor to the "version" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension "version"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        production {
            dimension "version"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/org.androidannotations.plugin.AndroidAnnotationsPlugin'
        exclude 'asm-license.txt'
    }
    buildToolsVersion = '29.0.2'
    if (project.hasProperty("hexiwear.properties") && file(project.property("hexiwear.properties")).exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(file(project.property("hexiwear.properties"))))

        signingConfigs {
            release {
                storeFile file(properties['keystore'])
                storePassword properties['keystore.password']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }


    }
}

def SupportVersion = '28.0.0'
def AAVersion = "4.6.0"
def ParcelerVersion = '1.1.12'
def RestAndroidVersion = '1.1.9'

dependencies {
    // Android support libraries
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    // Android annotations
    annotationProcessor "org.androidannotations:androidannotations:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"
    implementation "org.androidannotations:rest-spring-api:$AAVersion"

    // Parceler
    implementation "org.parceler:parceler-api:$ParcelerVersion"
    annotationProcessor "org.parceler:parceler:$ParcelerVersion"

    debugImplementation "com.wolkabout:restandroid:${RestAndroidVersion}.STAGING"
    implementation "com.wolkabout:restandroid:${RestAndroidVersion}.PRODUCTION"

    implementation 'com.wolkabout:wolk:1.0.2'

    implementation 'org.springframework.android:spring-android-rest-template:2.0.0.M3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'

    debugImplementation 'com.google.firebase:firebase-crash:16.2.1'
}
